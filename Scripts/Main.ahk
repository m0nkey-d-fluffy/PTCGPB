#Include %A_ScriptDir%\Include\Logging.ahk
#Include %A_ScriptDir%\Include\ADB.ahk
#Include %A_ScriptDir%\Include\Gdip_All.ahk
#Include %A_ScriptDir%\Include\Gdip_Imagesearch.ahk

#Include *i %A_ScriptDir%\Include\Gdip_Extra.ahk
#Include *i %A_ScriptDir%\Include\StringCompare.ahk
#Include *i %A_ScriptDir%\Include\OCR.ahk

#Include %A_ScriptDir%\Include\Utils.ahk
#Include %A_ScriptDir%\Include\Database.ahk
#Include %A_ScriptDir%\Include\CardDetection.ahk
#Include %A_ScriptDir%\Include\WonderPickManager.ahk
#Include %A_ScriptDir%\Include\AccountManager.ahk
#Include %A_ScriptDir%\Include\FriendManager.ahk

#SingleInstance on
;SetKeyDelay, -1, -1
SetMouseDelay, -1
SetDefaultMouseSpeed, 0
;SetWinDelay, -1
;SetControlDelay, -1
SetBatchLines, -1
SetTitleMatchMode, 3
CoordMode, Pixel, Screen

; Allocate and hide the console window to reduce flashing
DllCall("AllocConsole")
WinHide % "ahk_id " DllCall("GetConsoleWindow", "ptr")

global winTitle, changeDate, failSafe, openPack, Delay, failSafeTime, StartSkipTime, Columns, failSafe, scriptName, GPTest, StatusText, defaultLanguage, setSpeed, jsonFileName, pauseToggle, SelectedMonitorIndex, swipeSpeed, godPack, scaleParam, skipInvalidGP, deleteXML, packs, FriendID, AddFriend, Instances, showStatus
global triggerTestNeeded, testStartTime, firstRun, minStars, minStarsA2b, vipIdsURL
global autoUseGPTest, autotest, autotest_time, A_gptest, TestTime
global MuMuv5, titleHeight
MuMuv5 := isMuMuv5()

; Initialize titleHeight based on MuMuv5
if (MuMuv5) {
    titleHeight := 50
} else {
    titleHeight := 45
}

deleteAccount := false
scriptName := StrReplace(A_ScriptName, ".ahk")
winTitle := scriptName
pauseToggle := false
showStatus := true
jsonFileName := A_ScriptDir . "\..\json\Packs.json"
IniRead, FriendID, %A_ScriptDir%\..\Settings.ini, UserSettings, FriendID
IniRead, Instances, %A_ScriptDir%\..\Settings.ini, UserSettings, Instances
IniRead, Delay, %A_ScriptDir%\..\Settings.ini, UserSettings, Delay, 250
IniRead, folderPath, %A_ScriptDir%\..\Settings.ini, UserSettings, folderPath, C:\Program Files\Netease
IniRead, Variation, %A_ScriptDir%\..\Settings.ini, UserSettings, Variation, 20
IniRead, Columns, %A_ScriptDir%\..\Settings.ini, UserSettings, Columns, 5
IniRead, openPack, %A_ScriptDir%\..\Settings.ini, UserSettings, openPack, 1
IniRead, setSpeed, %A_ScriptDir%\..\Settings.ini, UserSettings, setSpeed, 2x
IniRead, defaultLanguage, %A_ScriptDir%\..\Settings.ini, UserSettings, defaultLanguage, Scale125
IniRead, SelectedMonitorIndex, %A_ScriptDir%\..\Settings.ini, UserSettings, SelectedMonitorIndex, 1:
IniRead, swipeSpeed, %A_ScriptDir%\..\Settings.ini, UserSettings, swipeSpeed, 350
IniRead, skipInvalidGP, %A_ScriptDir%\..\Settings.ini, UserSettings, skipInvalidGP, No
IniRead, godPack, %A_ScriptDir%\..\Settings.ini, UserSettings, godPack, Continue
IniRead, deleteMethod, %A_ScriptDir%\..\Settings.ini, UserSettings, deleteMethod, Hoard
IniRead, sendXML, %A_ScriptDir%\..\Settings.ini, UserSettings, sendXML, 0
IniRead, heartBeat, %A_ScriptDir%\..\Settings.ini, UserSettings, heartBeat, 1
if(heartBeat)
    IniWrite, 1, %A_ScriptDir%\..\HeartBeat.ini, HeartBeat, Main
IniRead, vipIdsURL, %A_ScriptDir%\..\Settings.ini, UserSettings, vipIdsURL
IniRead, ocrLanguage, %A_ScriptDir%\..\Settings.ini, UserSettings, ocrLanguage, en
IniRead, clientLanguage, %A_ScriptDir%\..\Settings.ini, UserSettings, clientLanguage, en
IniRead, minStars, %A_ScriptDir%\..\Settings.ini, UserSettings, minStars, 0
IniRead, minStarsA2b, %A_ScriptDir%\..\Settings.ini, UserSettings, minStarsA2b, 0

IniRead, autoUseGPTest, %A_ScriptDir%\..\Settings.ini, UserSettings, autoUseGPTest, 0
IniRead, TestTime, %A_ScriptDir%\..\Settings.ini, UserSettings, TestTime, 3600
global MuMuv5
MuMuv5 := isMuMuv5()
; connect adb
instanceSleep := scriptName * 1000
Sleep, %instanceSleep%

; Attempt to connect to ADB
ConnectAdb(folderPath)
Sleep, 2000
CreateStatusMessage("Disabling background services...")
DisableBackgroundServices()
Sleep, 5000

if (InStr(defaultLanguage, "100")) {
    scaleParam := 287
} else {
     	if (MuMuv5) {
			scaleParam := 283
		} else {
			scaleParam := 277
		}
}

resetWindows()
MaxRetries := 10
RetryCount := 0
Loop {
    try {
        WinGetPos, x, y, Width, Height, %winTitle%
        sleep, 2000
        ;Winset, Alwaysontop, On, %winTitle%
        OwnerWND := WinExist(winTitle)
        x4 := x + 4
        y4 := y + Height - 4 + 2
        buttonWidth := 45

        Gui, ToolBar:New, +Owner%OwnerWND% -AlwaysOnTop +ToolWindow -Caption +LastFound -DPIScale 
        Gui, ToolBar:Default
        Gui, ToolBar:Margin, 4, 4  ; Set margin for the GUI
        Gui, ToolBar:Font, s5 cGray Norm Bold, Segoe UI  ; Normal font for input labels
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 0) . " y0 w" . buttonWidth . " h25 gReloadScript", Reload  (Shift+F5)
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 1) . " y0 w" . buttonWidth . " h25 gPauseScript", Pause (Shift+F6)
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 2) . " y0 w" . buttonWidth . " h25 gResumeScript", Resume (Shift+F6)
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 3) . " y0 w" . buttonWidth . " h25 gStopScript", Stop (Shift+F7)
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 4) . " y0 w" . buttonWidth . " h25 gShowStatusMessages", Status (Shift+F8)
        Gui, ToolBar:Add, Button, % "x" . (buttonWidth * 5) . " y0 w" . buttonWidth . " h25 gTestScript", GP Test (Shift+F9)
        DllCall("SetWindowPos", "Ptr", WinExist(), "Ptr", 1  ; HWND_BOTTOM
                , "Int", 0, "Int", 0, "Int", 0, "Int", 0, "UInt", 0x13)  ; SWP_NOSIZE, SWP_NOMOVE, SWP_NOACTIVATE
        Gui, ToolBar:Show, NoActivate x%x4% y%y4%  w275 h30
        break
    }
    catch {
        RetryCount++
        if (RetryCount >= MaxRetries) {
            CreateStatusMessage("Failed to create button GUI.",,,, false)
            break
        }
        Sleep, 1000
    }
    Sleep, %Delay%
    CreateStatusMessage("Creating button GUI...",,,, false)
}

rerollTime := A_TickCount
autotest := A_TickCount
A_gptest := 0

initializeAdbShell()
CreateStatusMessage("Initializing bot...",,,, false)
restartGameInstance("Initializing bot...", false)
pToken := Gdip_Startup()

if(heartBeat)
    IniWrite, 1, %A_ScriptDir%\..\HeartBeat.ini, HeartBeat, Main
FindImageAndClick(120, 500, 155, 530, , "Social", 143, 518, 1000, 150)
firstRun := true

global 99Configs := {}
99Configs["en"] := {leftx: 123, rightx: 162}
99Configs["es"] := {leftx: 68, rightx: 107}
99Configs["fr"] := {leftx: 56, rightx: 95}
99Configs["de"] := {leftx: 72, rightx: 111}
99Configs["it"] := {leftx: 60, rightx: 99}
99Configs["pt"] := {leftx: 127, rightx: 166}
99Configs["jp"] := {leftx: 84, rightx: 127}
99Configs["ko"] := {leftx: 65, rightx: 100}
99Configs["cn"] := {leftx: 63, rightx: 102}

99Path := "99" . clientLanguage
99Leftx := 99Configs[clientLanguage].leftx
99Rightx := 99Configs[clientLanguage].rightx

Loop {
    if (autoUseGPTest) {
        autotest_time := (A_TickCount - autotest) // 1000
        CreateStatusMessage("Auto GP Test Timer : " . autotest_time .  "/ " . TestTime . " seconds", "AutoGPTest", 0, 605, false, true)
        if (autotest_time >= TestTime) {
            A_gptest := 1
            ToggleTestScript()
        }        
    }

    if (GPTest) {
        if (triggerTestNeeded)
            GPTestScript()
        Sleep, 1000
        if (heartBeat && (Mod(A_Index, 60) = 0))
            IniWrite, 1, %A_ScriptDir%\..\HeartBeat.ini, HeartBeat, Main
        Continue
    }

    if(heartBeat)
        IniWrite, 1, %A_ScriptDir%\..\HeartBeat.ini, HeartBeat, Main
    Sleep, %Delay%
    FindImageAndClick(120, 500, 155, 530, , "Social", 143, 518, 1000, 30)
    FindImageAndClick(226, 100, 270, 135, , "Add", 38, 460, 500)
    FindImageAndClick(170, 450, 195, 480, , "Approve", 228, 464)
    /* ; Deny all option
    if(firstRun) {
        Sleep, 1000
        adbClick(205, 510)
        Sleep, 1000
        adbClick(210, 372)
        firstRun := false
    }
    */
    done := false
    Loop 3 {
        Sleep, %Delay%
        if(FindOrLoseImage(225, 195, 250, 215, , "Pending", 0)) {
            failSafe := A_TickCount
            failSafeTime := 0
            Loop {
                Sleep, %Delay%
                clickButton := FindOrLoseImage(75, 340, 195, 530, 80, "Button", 0, failSafeTime) ;looking for ok button in case an invite is withdrawn
                if(FindOrLoseImage(99Leftx, 110, 99Rightx, 127, , 99Path, 0, failSafeTime)) {
                    done := true
                    break
                } else if(FindOrLoseImage(225, 195, 250, 220, , "Pending", 0, failSafeTime)) {
                    if (GPTest)
                        break
                    adbClick(245, 210)
                } else if(FindOrLoseImage(186, 496, 206, 518, , "Accept", 0, failSafeTime)) {
                    done := true
                    break
                } else if(FindOrLoseImage(120, 187, 155, 210, , "Error", 0, failSafeTime)) {
                    ; Handle communication error
                    CreateStatusMessage("Error message detected. Clicking retry...",,,, false)
                    LogToFile("Error message in Main " . scriptName . ". Clicking retry...")
                    Sleep, 1000
                    adbClick(82, 389)  ; Click retry button
                    Sleep, 1000
                    adbClick(139, 386) ; Click OK/confirm
                    Sleep, 1000
                    Reload
                } else if(FindOrLoseImage(124, 423, 155, 455, , "StartupErrorX", 0, failSafeTime)) {
                    ; Handle startup error with X button
                    CreateStatusMessage("Start-up error detected. Clearing and reloading...",,,, false)
                    LogToFile("Start-up error in Main " . scriptName . ". Reloading...")
                    Sleep, 2000
                    adbClick(139, 440)  ; Click X to close error
                    Sleep, 4000
                    Reload
                } else if(clickButton) {
                    StringSplit, pos, clickButton, `,  ; Split at ", "
                    if (scaleParam = 287) {
                        pos2 += 5
                    }
                    Sleep, 1000
                    if(FindImageAndClick(190, 195, 215, 220, , "DeleteFriend", pos1, pos2, 4000)) {
                        Sleep, %Delay%
                        adbClick(210, 210)
                    }
                }
                if (GPTest)
                    break
                failSafeTime := (A_TickCount - failSafe) // 1000
                CreateStatusMessage("Failsafe " . failSafeTime . "/180 seconds")
            }
        }
        if(done || fullList|| GPTest)
            break
    }
}
return

FindOrLoseImage(X1, Y1, X2, Y2, searchVariation := "", imageName := "DEFAULT", EL := 1, safeTime := 0) {
    global winTitle, Variation, failSafe
    if(searchVariation = "")
        searchVariation := Variation
    imagePath := A_ScriptDir . "\" . defaultLanguage . "\"
    confirmed := false

    ; MuMuv5 image search Y offset adjustment
    yBias := titleHeight - 45
    Y1 += yBias
    Y2 += yBias

    CreateStatusMessage("Finding " . imageName . "...")
    pBitmap := from_window(WinExist(winTitle))
    Path = %imagePath%%imageName%.png
    pNeedle := GetNeedle(Path)

    ; 100% scale changes
    if (scaleParam = 287) {
        Y1 -= 8 ; offset, should be 44-36 i think?
        Y2 -= 8
        if (Y1 < 0) {
            Y1 := 0
        }
        if (imageName = "Bulba") { ; too much to the left? idk how that happens
            X1 := 200
            Y1 := 220
            X2 := 230
            Y2 := 260
        }else if (imageName = 99Path) { ; 100% full of friend list
            Y1 := 103
            Y2 := 118
        }
    }
    ;bboxAndPause(X1, Y1, X2, Y2)

    ; ImageSearch within the region
    vRet := Gdip_ImageSearch(pBitmap, pNeedle, vPosXY, X1, Y1, X2, Y2, searchVariation)
    Gdip_DisposeImage(pBitmap)
    if(EL = 0)
        GDEL := 1
    else
        GDEL := 0
    if (!confirmed && vRet = GDEL && GDEL = 1) {
        confirmed := vPosXY
    } else if(!confirmed && vRet = GDEL && GDEL = 0) {
        confirmed := true
    }
    pBitmap := from_window(WinExist(winTitle))
    Path = %imagePath%App.png
    if (MuMuv5)
        Path = %imagePath%App2.png
    pNeedle := GetNeedle(Path)
    ; ImageSearch within the region
    vRet := Gdip_ImageSearch(pBitmap, pNeedle, vPosXY, 15, 155, 270, 420, searchVariation)
    Gdip_DisposeImage(pBitmap)
    if (vRet = 1) {
        LogToFile("Stuck at home while looking for " . imageName . "...")
        restartGameInstance("Stuck at " . imageName . "...")
    }
    if(imageName = "Country" || imageName = "Social")
        FSTime := 90
    else if(imageName = "Button")
        FSTime := 240
    else
        FSTime := 180
    if (safeTime >= FSTime) {
        LogToFile("Instance " . scriptName . " has been stuck at " . imageName . " for 90s. (EL: " . EL . ", sT: " . safeTime . ") Killing it...")
        restartGameInstance("Stuck at " . imageName . "...")
        failSafe := A_TickCount
    }
    return confirmed
}

FindImageAndClick(X1, Y1, X2, Y2, searchVariation := "", imageName := "DEFAULT", clickx := 0, clicky := 0, sleepTime := "", skip := false, safeTime := 0) {
    global winTitle, Variation, failSafe, confirmed
    if(searchVariation = "")
        searchVariation := Variation
    if (sleepTime = "") {
        global Delay
        sleepTime := Delay
    }
    imagePath := A_ScriptDir . "\" defaultLanguage "\"
    click := false
    if(clickx > 0 and clicky > 0)
        click := true
    x := 0
    y := 0
    StartSkipTime := A_TickCount

    confirmed := false

    ; MuMuv5 image search Y offset adjustment
    yBias := titleHeight - 45
    Y1 += yBias
    Y2 += yBias

    ; 100% scale changes
    if (scaleParam = 287) {
        Y1 -= 8 ; offset, should be 44-36 i think?
        Y2 -= 8
        if (Y1 < 0) {
            Y1 := 0
        }

        if (imageName = "Platin") { ; can't do text so purple box
            X1 := 141
            Y1 := 189
            X2 := 208
            Y2 := 224
        } else if (imageName = "Opening") { ; Opening click (to skip cards) can't click on the immersive skip with 239, 497
            clickx := 250
            clicky := 505
        }
    }

    if(click) {
        adbClick(clickx, clicky)
        clickTime := A_TickCount
    }
    CreateStatusMessage("Finding and clicking " . imageName . "...")

    Loop { ; Main loop
        Sleep, 100
        if(click) {
            ElapsedClickTime := A_TickCount - clickTime
            if(ElapsedClickTime > sleepTime) {
                adbClick(clickx, clicky)
                clickTime := A_TickCount
            }
        }

        if (confirmed) {
            continue
        }

        pBitmap := from_window(WinExist(winTitle))
        Path = %imagePath%%imageName%.png
        pNeedle := GetNeedle(Path)
        ;bboxAndPause(X1, Y1, X2, Y2)
        ; ImageSearch within the region
        vRet := Gdip_ImageSearch(pBitmap, pNeedle, vPosXY, X1, Y1, X2, Y2, searchVariation)
        Gdip_DisposeImage(pBitmap)
        if (!confirmed && vRet = 1) {
            confirmed := vPosXY
        } else {
            if(skip < 45) {
                ElapsedTime := (A_TickCount - StartSkipTime) // 1000
                FSTime := 45
                if (ElapsedTime >= FSTime || safeTime >= FSTime) {
                    LogToFile("Instance " . scriptName . " has been stuck at " . imageName . " for 90s. (EL: " . ElapsedTime . ", sT: " . safeTime . ") Killing it...")
                    restartGameInstance("Stuck at " . imageName . "...") ; change to reset the instance and delete data then reload script
                    StartSkipTime := A_TickCount
                    failSafe := A_TickCount
                }
            }
        }

        pBitmap := from_window(WinExist(winTitle))
        Path = %imagePath%Error1.png
        pNeedle := GetNeedle(Path)
        ; ImageSearch within the region
        vRet := Gdip_ImageSearch(pBitmap, pNeedle, vPosXY, 15, 155, 270, 420, searchVariation)
        Gdip_DisposeImage(pBitmap)
        if (vRet = 1) {
            CreateStatusMessage("Error message in " . scriptName . ". Clicking retry...")
            LogToFile("Error message in " . scriptName . ". Clicking retry...")
            adbClick(82, 389)
            Sleep, %Delay%
            adbClick(139, 386)
            Sleep, 1000
        }
        pBitmap := from_window(WinExist(winTitle))
        Path = %imagePath%App.png
        if (MuMuv5)
        Path = %imagePath%App2.png
        pNeedle := GetNeedle(Path)
        ; ImageSearch within the region
        vRet := Gdip_ImageSearch(pBitmap, pNeedle, vPosXY, 15, 155, 270, 420, searchVariation)
        Gdip_DisposeImage(pBitmap)
        if (vRet = 1) {
            LogToFile("At the home page while looking for " . imageName . "...")
            restartGameInstance("At the home page while looking for " . imageName . "...")
        }

        if(skip) {
            ElapsedTime := (A_TickCount - StartSkipTime) // 1000
            if (ElapsedTime >= skip) {
                return false
                ElapsedTime := ElapsedTime/2
                break
            }
        }
        if (confirmed) {
            break
        }

    }
    return confirmed
}

resetWindows(){
    global Columns, winTitle, SelectedMonitorIndex, scaleParam, titleHeight, MuMuv5
    CreateStatusMessage("Arranging window positions and sizes")
    RetryCount := 0
    MaxRetries := 10
    Loop {
        try {
            SelectedMonitorIndex := RegExReplace(SelectedMonitorIndex, ":.*$")
            SysGet, Monitor, Monitor, %SelectedMonitorIndex%
            Title := winTitle

            instanceIndex := StrReplace(Title, "Main", "")
            if (instanceIndex = "")
                instanceIndex := 1

                    
            if (MuMuv5) {
                titleHeight := 50
            } else {
                titleHeight := 45
            }
            
            borderWidth := 4 - 1
            rowHeight := titleHeight + 489 + 4
            currentRow := Floor((instanceIndex - 1) / Columns)

            y := MonitorTop + (currentRow * rowHeight) + (currentRow * rowGap)
            ;x := MonitorLeft + (Mod((instanceIndex - 1), Columns) * scaleParam)
            if (MuMuv5) {
                x := MonitorLeft + (Mod((instanceIndex - 1), Columns) * (scaleParam - borderWidth * 2)) - borderWidth
            } else {
                x := MonitorLeft + (Mod((instanceIndex - 1), Columns) * scaleParam)
            }
            
            WinSet, Style, -0xC00000, %Title%
            WinMove, %Title%, , %x%, %y%, %scaleParam%, %rowHeight%
            WinSet, Style, +0xC00000, %Title%
            WinSet, Redraw, , %Title%
            break
        }
        catch {
            if (RetryCount > MaxRetries)
                CreateStatusMessage("Pausing. Can't find window " . winTitle . ".",,,, false)
            Pause
        }
        Sleep, 1000
    }
    return true
}

restartGameInstance(reason, RL := true){
    global Delay, scriptName, adbShell, adbPath, adbPort
    initializeAdbShell()

    if (Debug)
        CreateStatusMessage("Restarting game reason:`n" . reason)
    else
        CreateStatusMessage("Restarting game...",,,, false)

    adbWriteRaw("am force-stop jp.pokemon.pokemontcgp")
    ;adbShell.StdIn.WriteLine("rm -rf /data/data/jp.pokemon.pokemontcgp/cache/*") ; clear cache
    Sleep, 3000
    adbWriteRaw("am start -n jp.pokemon.pokemontcgp/com.unity3d.player.UnityPlayerActivity")

    Sleep, 3000
    if(RL) {
        LogToFile("Restarted game for instance " . scriptName . ". Reason: " reason, "Restart.txt")
        Reload
    }
}

ControlClick(X, Y) {
    global winTitle
    ControlClick, x%X% y%Y%, %winTitle%
}

RandomUsername() {
    FileRead, content, %A_ScriptDir%\..\usernames.txt

    values := StrSplit(content, "`r`n") ; Use `n if the file uses Unix line endings

    ; Get a random index from the array
    Random, randomIndex, 1, values.MaxIndex()

    ; Return the random value
    return values[randomIndex]
}

Screenshot(fileType := "Valid", subDir := "", ByRef fileName := "") {
    global adbShell, adbPath, packs, winTitle, titleHeight, scaleParam, packsInPool
    SetWorkingDir %A_ScriptDir%  ; Ensures the working directory is the script's directory

    ; Define folder and file paths
    fileDir := A_ScriptDir "\..\Screenshots"
    if !FileExist(fileDir)
        FileCreateDir, %fileDir%
    if (subDir) {
        fileDir .= "\" . subDir
		if !FileExist(fileDir)
			FileCreateDir, %fileDir%
    }
	if (fileType = "PACKSTATS") {
        fileDir .= "\temp"
		if !FileExist(fileDir)
			FileCreateDir, %fileDir%
	}

    ; File path for saving the screenshot locally
    fileName := A_Now . "_" . winTitle . "_" . fileType . "_" . packsInPool . "_packs.png"
    if (fileType = "PACKSTATS")
        fileName := "packstats_temp.png"
    filePath := fileDir "\" . fileName

    yBias := titleHeight - 45
    pBitmapW := from_window(WinExist(winTitle))
    pBitmap := Gdip_CloneBitmapArea(pBitmapW, 18, 175+yBias, 240, 227)

    ;scale 100%
    if (scaleParam = 287) {
        pBitmap := Gdip_CloneBitmapArea(pBitmapW, 17, 168, 245, 230)
    }
    Gdip_DisposeImage(pBitmapW)
    Gdip_SaveBitmapToFile(pBitmap, filePath)

    ; Don't dispose pBitmap if it's a PACKSTATS screenshot
    if (fileType != "PACKSTATS") {
        Gdip_DisposeImage(pBitmap)
		return filePath
    }

    ; For PACKSTATS, return both values and delete temp file after OCR is done
    return {filepath: filePath, bitmap: pBitmap, deleteAfterUse: true}
}

; Pause Script
PauseScript:
    CreateStatusMessage("Pausing...",,,, false)
    Pause, On
return

; Resume Script
ResumeScript:
    CreateStatusMessage("Resuming...",,,, false)
    Pause, Off
    StartSkipTime := A_TickCount ;reset stuck timers
    failSafe := A_TickCount
return

; Stop Script
StopScript:
    CreateStatusMessage("Stopping script...",,,, false)
ExitApp
return

ShowStatusMessages:
    ToggleStatusMessages()
return

ReloadScript:
    Reload
return

TestScript:
    ToggleTestScript()
return

ToggleTestScript() {
    global GPTest, triggerTestNeeded, testStartTime, firstRun
    if(!GPTest) {
        GPTest := true
        triggerTestNeeded := true
        testStartTime := A_TickCount
        CreateStatusMessage("In GP Test Mode",,,, false)
        StartSkipTime := A_TickCount ;reset stuck timers
        failSafe := A_TickCount
    }
    else {
        GPTest := false
        triggerTestNeeded := false
        totalTestTime := (A_TickCount - testStartTime) // 1000
        if (testStartTime != "" && (totalTestTime >= 180))
        {
            firstRun := True
            testStartTime := ""
        }
        CreateStatusMessage("Exiting GP Test Mode",,,, false)
    }
}

FriendAdded() {
    global AddFriend
    AddFriend++
}

; Function to create or select the JSON file
InitializeJsonFile() {
    global jsonFileName
    fileName := A_ScriptDir . "\..\json\Packs.json"
    if !FileExist(fileName) {
        ; Create a new file with an empty JSON array
        FileAppend, [], %fileName%  ; Write an empty JSON array
        jsonFileName := fileName
        return
    }
}

; Function to sum all variable values in the JSON file
SumVariablesInJsonFile() {
    global jsonFileName
    if (jsonFileName = "") {
        return 0
    }

    ; Read the file content
    FileRead, jsonContent, %jsonFileName%
    if (jsonContent = "") {
        return 0
    }

    ; Parse the JSON and calculate the sum
    sum := 0
    ; Clean and parse JSON content
    jsonContent := StrReplace(jsonContent, "[", "") ; Remove starting bracket
    jsonContent := StrReplace(jsonContent, "]", "") ; Remove ending bracket
    Loop, Parse, jsonContent, {, }
    {
        ; Match each variable value
        if (RegExMatch(A_LoopField, """variable"":\s*(-?\d+)", match)) {
            sum += match1
        }
    }

    ; Write the total sum to a file called "total.json"
    totalFile := A_ScriptDir . "\json\total.json"
    totalContent := "{""total_sum"": " sum "}"
    FileDelete, %totalFile%
    FileAppend, %totalContent%, %totalFile%

    return sum
}

from_window(ByRef image) {
    ; Thanks tic - https://www.autohotkey.com/boards/viewtopic.php?t=6517

    ; Get the handle to the window.
    image := (hwnd := WinExist(image)) ? hwnd : image

    ; Restore the window if minimized! Must be visible for capture.
    if DllCall("IsIconic", "ptr", image)
        DllCall("ShowWindow", "ptr", image, "int", 4)

    ; Get the width and height of the client window.
    VarSetCapacity(Rect, 16) ; sizeof(RECT) = 16
    DllCall("GetClientRect", "ptr", image, "ptr", &Rect)
        , width  := NumGet(Rect, 8, "int")
        , height := NumGet(Rect, 12, "int")

    ; struct BITMAPINFOHEADER - https://docs.microsoft.com/en-us/windows/win32/api/wingdi/ns-wingdi-bitmapinfoheader
    hdc := DllCall("CreateCompatibleDC", "ptr", 0, "ptr")
    VarSetCapacity(bi, 40, 0)                ; sizeof(bi) = 40
        , NumPut(       40, bi,  0,   "uint") ; Size
        , NumPut(    width, bi,  4,   "uint") ; Width
        , NumPut(  -height, bi,  8,    "int") ; Height - Negative so (0, 0) is top-left.
        , NumPut(        1, bi, 12, "ushort") ; Planes
        , NumPut(       32, bi, 14, "ushort") ; BitCount / BitsPerPixel
        , NumPut(        0, bi, 16,   "uint") ; Compression = BI_RGB
        , NumPut(        3, bi, 20,   "uint") ; Quality setting (3 = low quality, no anti-aliasing)
    hbm := DllCall("CreateDIBSection", "ptr", hdc, "ptr", &bi, "uint", 0, "ptr*", pBits:=0, "ptr", 0, "uint", 0, "ptr")
    obm := DllCall("SelectObject", "ptr", hdc, "ptr", hbm, "ptr")

    ; Print the window onto the hBitmap using an undocumented flag. https://stackoverflow.com/a/40042587
    DllCall("PrintWindow", "ptr", image, "ptr", hdc, "uint", 0x3) ; PW_CLIENTONLY | PW_RENDERFULLCONTENT
    ; Additional info on how this is implemented: https://www.reddit.com/r/windows/comments/8ffr56/altprintscreen/

    ; Convert the hBitmap to a Bitmap using a built in function as there is no transparency.
    DllCall("gdiplus\GdipCreateBitmapFromHBITMAP", "ptr", hbm, "ptr", 0, "ptr*", pBitmap:=0)

    ; Cleanup the hBitmap and device contexts.
    DllCall("SelectObject", "ptr", hdc, "ptr", obm)
    DllCall("DeleteObject", "ptr", hbm)
    DllCall("DeleteDC",     "ptr", hdc)

    return pBitmap
}

~+F5::Reload
~+F6::Pause
~+F7::ExitApp
~+F8::ToggleStatusMessages()
~+F9::ToggleTestScript() ; hoytdj Add

ToggleStatusMessages() {
    if(showStatus)
        showStatus := False
    else
        showStatus := True
}

bboxAndPause(X1, Y1, X2, Y2, doPause := False) {
    BoxWidth := X2-X1
    BoxHeight := Y2-Y1
    ; Create a GUI
    Gui, BoundingBox:+AlwaysOnTop +ToolWindow -Caption +E0x20
    Gui, BoundingBox:Color, 123456
    Gui, BoundingBox:+LastFound  ; Make the GUI window the last found window for use by the line below. (straght from documentation)
    WinSet, TransColor, 123456 ; Makes that specific color transparent in the gui

    ; Create the borders and show
    Gui, BoundingBox:Add, Progress, x0 y0 w%BoxWidth% h2 BackgroundRed
    Gui, BoundingBox:Add, Progress, x0 y0 w2 h%BoxHeight% BackgroundRed
    Gui, BoundingBox:Add, Progress, x%BoxWidth% y0 w2 h%BoxHeight% BackgroundRed
    Gui, BoundingBox:Add, Progress, x0 y%BoxHeight% w%BoxWidth% h2 BackgroundRed
    Gui, BoundingBox:Show, x%X1% y%Y1% NoActivate
    Sleep, 100

    if (doPause) {
        Pause
    }

    if GetKeyState("F4", "P") {
        Pause
    }

    Gui, BoundingBox:Destroy
}

GetNeedle(Path) {
    static NeedleBitmaps := Object()
    if (NeedleBitmaps.HasKey(Path)) {
        return NeedleBitmaps[Path]
    } else {
        pNeedle := Gdip_CreateBitmapFromFile(Path)
        NeedleBitmaps[Path] := pNeedle
        return pNeedle
    }
}

; ^e::
; msgbox ss
; pToken := Gdip_Startup()
; Screenshot()
; return

; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; ~~~ GP Test Mode Everying Below ~~~
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

GPTestScript() {
    global triggerTestNeeded
    triggerTestNeeded := false

    ; Check if remove_users.txt exists - if so, use the new removal with wonderpick method
    removeListFile := A_ScriptDir . "\..\remove_users.txt"
    if (FileExist(removeListFile)) {
        RemoveUsersFromListWithWonderPick()
    } else {
        ; Fall back to original VIP-based removal
        RemoveNonVipFriends()
    }
}

; Automation script for removing Non-VIP firends.
RemoveNonVipFriends() {
    global GPTest, vipIdsURL, failSafe
    failSafe := A_TickCount
    failSafeTime := 0
    ; Get us to the Social screen. Won't be super resilient but should be more consistent for most cases.
    Loop {
        adbClick(143, 518)
        if(FindOrLoseImage(120, 500, 155, 530, , "Social", 0, failSafeTime))
            break
        Delay(5)
        failSafeTime := (A_TickCount - failSafe) // 1000
        CreateStatusMessage("In failsafe for Social. " . failSafeTime "/90 seconds")
    }
    FindImageAndClick(226, 100, 270, 135, , "Add", 38, 460, 500)
    Delay(3)

    CreateStatusMessage("Downloading vip_ids.txt.",,,, false)
    if (vipIdsURL != "" && !DownloadFile(vipIdsURL, "vip_ids.txt")) {
        CreateStatusMessage("Failed to download vip_ids.txt..`nIf you are botting solo, disable AutoGPTest to fix this issue.`nAborting test...",,,, false)
        return
    }

    includesIdsAndNames := false
    vipFriendsArray :=  GetFriendAccountsFromFile(A_ScriptDir . "\..\vip_ids.txt", includesIdsAndNames)
    
		; append new list onto vipFriendsArray to combine manual and automatic GPtesting KSBM
		manualVipFile := A_ScriptDir . "\..\manual_vip_ids.txt"
		if FileExist(manualVipFile) {
		    ManualvipFriendsArray := GetFriendAccountsFromFile(manualVipFile, includesIdsAndNames)
		    vipFriendsArray.push(ManualvipFriendsArray*)
		}                
    
    if (!vipFriendsArray.MaxIndex()) {
        CreateStatusMessage("No accounts found in vip_ids.txt. Aborting test...",,,, false)
        return
    }

    friendIndex := 0
    repeatFriendAccounts := 0
	scrolledWithoutFriend := 0
    recentFriendAccounts := []
    Loop {
        if (scrolledWithoutFriend > 5){
            CreateStatusMessage("End of list - scrolled without friend codes multiple times.`nReady to test.")
            if(A_gptest && autoUseGPTest) {
                A_gptest := 0
                autotest := A_TickCount
                ToggleTestScript()
			}
            return
        }
        friendClickY := 195 + (95 * friendIndex)
        if (FindImageAndClick(75, 400, 105, 420, , "Friend", 138, friendClickY, 500, 3)) {
            Delay(1)

            ; Get the friend account
            parseFriendResult := ParseFriendInfo(friendCode, friendName, parseFriendCodeResult, parseFriendNameResult, includesIdsAndNames)
            friendAccount := new FriendAccount(friendCode, friendName)

            ; Check if this is a repeat
            if (IsRecentlyCheckedAccount(friendAccount, recentFriendAccounts)) {
                repeatFriendAccounts++
            }
            else if (parseFriendResult) {
                repeatFriendAccounts := 0
            }
            if (repeatFriendAccounts > 2) {
                if (Debug)
                    CreateStatusMessage("End of list - parsed the same friend codes multiple times.`nReady to test.")
                else
                    CreateStatusMessage("Ready to test.",,,, false)
                adbClick(143, 507)
                if(A_gptest && autoUseGPTest) {
					A_gptest := 0
					autotest := A_TickCount
					ToggleTestScript()
				}
                return
            }
            matchedFriend := ""
            isVipResult := IsFriendAccountInList(friendAccount, vipFriendsArray, matchedFriend)
            if (isVipResult || !parseFriendResult) {
                ; If we couldn't parse the friend, skip removal
                if (!parseFriendResult) {
                    CreateStatusMessage("Couldn't parse friend. Skipping friend...`nParsed friend: " . friendAccount.ToString(),,,, false)
                    LogToFile("Friend skipped: " . friendAccount.ToString() . ". Couldn't parse identifiers.", "GPTestLog.txt")
                }
                ; If it's a VIP friend, skip removal
                if (isVipResult) {
                    CreateStatusMessage("Parsed friend: " . friendAccount.ToString() . "`nMatched VIP: " . matchedFriend.ToString() . "`nSkipping VIP...",,,, false)
					scrolledWithoutFriend := 0
				}
                Sleep, 1500 ; Time to read
                FindImageAndClick(226, 100, 270, 135, , "Add", 143, 507, 500)
                Delay(2)
                if (friendIndex < 2)
                    friendIndex++
                else {
                    ; Large vertical swipe up, to scroll through no more than 3 friends on the friend list.
                    ; The swipe is performed with a fixed X-coordinate, simulating a larger vertical swipe.
                    X := 138
                    Y1 := 380
                    Y2 := 200

                    Delay(10)
                    adbSwipe(X . " " . Y1 . " " . X . " " . Y2 . " " . 300)
                    Sleep, 1000

                    friendIndex := 0
                }
            }
            else {
                ; If NOT a VIP remove the friend
                CreateStatusMessage("Parsed friend: " . friendAccount.ToString() . "`nNo VIP match found.`nRemoving friend...",,,, false)
                LogToFile("Friend removed: " . friendAccount.ToString() . ". No VIP match found.", "GPTestLog.txt")
                Sleep, 1500 ; Time to read
                FindImageAndClick(135, 355, 160, 385, , "Remove", 145, 407, 500)
                FindImageAndClick(70, 395, 100, 420, , "Send2", 200, 372, 500)
                Delay(1)
                FindImageAndClick(226, 100, 270, 135, , "Add", 143, 507, 500)
                Delay(3)
				scrolledWithoutFriend := 0
            }
        }
        else {
            ; If on social screen, we're stuck between friends, micro scroll
            If (FindOrLoseImage(226, 100, 270, 135, , "Add", 0)) {
                CreateStatusMessage("Stuck between friends. Tiny scroll and continue.")

                ; Very small vertical swipe up, to correct miss-swipe on the friend list.
                ; The swipe is performed with a fixed X-coordinate, simulating a small vertical swipe.
                X := 138
                Y1 := 380
                Y2 := 355

                Delay(3)
                adbSwipe(X . " " . Y1 . " " . X . " " . Y2 . " " . 200)
                Sleep, 500
            }
            else { ; Handling for account not currently in use
                FindImageAndClick(226, 100, 270, 135, , "Add", 143, 508, 500)
                Delay(3)
            }
			scrolledWithoutFriend++
        }
        if (!GPTest) {
            Return
        }
    }
}

; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
; ~~~ GP Test Mode: Remove Users from List with WonderPick Check  ~~~
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

; RemoveUsersFromListWithWonderPick - Remove users by name from a list, checking wonderpick periodically
; Removes 4 users, then checks wonderpick 3 times, repeating until 40 users are removed
; IMPROVED: After each removal, exits and re-enters friend list, scrolls from top to find next match
RemoveUsersFromListWithWonderPick() {
    global GPTest, failSafe, scaleParam, winTitle

    ; Configuration
    usersPerCycle := 4          ; Remove this many users before checking wonderpick
    wonderpickChecks := 3       ; Check wonderpick this many times after each cycle
    maxRemovals := 40           ; Stop after this many total removals

    ; Read usernames from file
    removeListFile := A_ScriptDir . "\..\remove_users.txt"
    if (!FileExist(removeListFile)) {
        CreateStatusMessage("remove_users.txt not found!`nCreate the file with one username per line.",,,, false)
        LogToFile("RemoveUsersFromListWithWonderPick: remove_users.txt not found at " . removeListFile, "GPTestLog.txt")
        return
    }

    ; Load usernames into array
    removeList := []
    FileRead, fileContent, %removeListFile%
    Loop, Parse, fileContent, `n, `r
    {
        trimmedName := Trim(A_LoopField)
        if (trimmedName != "")
            removeList.Push(trimmedName)
    }

    if (removeList.Length() = 0) {
        CreateStatusMessage("remove_users.txt is empty!`nAdd usernames (one per line) to remove.",,,, false)
        LogToFile("RemoveUsersFromListWithWonderPick: remove_users.txt is empty", "GPTestLog.txt")
        return
    }

    CreateStatusMessage("Loaded " . removeList.Length() . " usernames to remove.`nTarget: " . maxRemovals . " removals",,,, false)
    LogToFile("RemoveUsersFromListWithWonderPick: Starting with " . removeList.Length() . " usernames to remove", "GPTestLog.txt")
    Sleep, 2000

    ; Tracking variables
    totalRemoved := 0
    cycleRemoved := 0

    ; Main removal loop - each iteration finds and removes ONE user
    Loop {
        ; Check if we've reached max removals
        if (totalRemoved >= maxRemovals) {
            CreateStatusMessage("Completed! Removed " . totalRemoved . " users.`nGP Test finished.",,,, false)
            LogToFile("RemoveUsersFromListWithWonderPick: Completed - removed " . totalRemoved . " users", "GPTestLog.txt")
            return
        }

        ; Check if it's time to do wonderpick checks (after every 4 removals)
        if (cycleRemoved >= usersPerCycle && totalRemoved > 0) {
            CreateStatusMessage("Removed " . cycleRemoved . " users this cycle.`nChecking WonderPick " . wonderpickChecks . " times...",,,, false)
            LogToFile("RemoveUsersFromListWithWonderPick: Cycle complete, checking wonderpick " . wonderpickChecks . " times", "GPTestLog.txt")
            Sleep, 1500

            ; Do wonderpick checks
            Loop, %wonderpickChecks% {
                currentCheck := A_Index
                CreateStatusMessage("WonderPick check " . currentCheck . "/" . wonderpickChecks,,,, false)
                DoWonderPickCheck()
                Sleep, 2000
            }

            cycleRemoved := 0
        }

        ; === FRESH START: Navigate to Social and enter friend list from scratch ===
        CreateStatusMessage("Entering friend list... (" . totalRemoved . "/" . maxRemovals . " removed)",,,, false)
        if (!GoToSocialScreen()) {
            CreateStatusMessage("Failed to navigate to Social. Aborting.",,,, false)
            return
        }

        ; Enter the friends list
        FindImageAndClick(226, 100, 270, 135, , "Add", 38, 460, 500)
        Delay(3)

        ; === SCROLL THROUGH ENTIRE LIST looking for a match ===
        matchFound := false
        scrollCount := 0
        maxScrolls := 30  ; Reduced from 50
        previousNames := ""

        Loop {
            if (scrollCount >= maxScrolls) {
                CreateStatusMessage("Reached scroll limit.",,,, false)
                break
            }

            ; Scan visible friends (3 at a time)
            CreateStatusMessage("Scanning... (scroll " . scrollCount . "/" . maxScrolls . ")`nRemoved: " . totalRemoved . "/" . maxRemovals,,,, false)
            visibleFriends := ScanFriendListNames()

            ; Build a string of current names to detect end of list
            currentNames := ""
            validNamesCount := 0
            for idx, friendInfo in visibleFriends {
                if (friendInfo.name != "") {
                    currentNames .= friendInfo.name . "|"
                    validNamesCount++
                }
            }

            ; Check if we've reached the end (same VALID names as before after scrolling)
            ; Only trigger if we have at least one valid name
            if (validNamesCount > 0 && currentNames = previousNames && scrollCount > 0) {
                CreateStatusMessage("Reached end of friend list (same names detected).",,,, false)
                LogToFile("RemoveUsersFromListWithWonderPick: End of list detected at scroll " . scrollCount, "GPTestLog.txt")
                break
            }

            ; If we got valid names, save them for next comparison
            if (validNamesCount > 0)
                previousNames := currentNames

            ; Check each visible friend against our removal list
            for idx, friendInfo in visibleFriends {
                parsedName := friendInfo.name
                if (parsedName = "")
                    continue

                ; Check against all names in removal list
                for listIdx, targetName in removeList {
                    if (FuzzyNameMatch(parsedName, targetName)) {
                        ; === FOUND A MATCH! Remove this friend ===
                        matchFound := true
                        clickY := friendInfo.clickY

                        CreateStatusMessage("MATCH: " . parsedName . "`n-> " . targetName . "`nRemoving... (" . (totalRemoved + 1) . "/" . maxRemovals . ")",,,, false)
                        LogToFile("RemoveUsersFromListWithWonderPick: Removing " . parsedName . " (matched " . targetName . ") - #" . (totalRemoved + 1), "GPTestLog.txt")
                        Sleep, 500

                        ; Click on the friend to open their profile
                        adbClick(138, clickY)
                        Delay(2)

                        ; Perform the removal
                        FindImageAndClick(135, 355, 160, 385, , "Remove", 145, 407, 500)
                        FindImageAndClick(70, 395, 100, 420, , "Send2", 200, 372, 500)
                        Delay(1)

                        totalRemoved++
                        cycleRemoved++

                        ; Exit all inner loops - will re-enter friend list fresh
                        break 3
                    }
                }
            }

            ; No match in current view, scroll down to see more
            ScrollFriendList()
            scrollCount++
            Sleep, 1500  ; Increased delay to let UI settle after scroll

            if (!GPTest)
                return
        }

        ; If we scrolled through entire list without finding a match, we're done
        if (!matchFound) {
            CreateStatusMessage("No more matching users in list.`nTotal removed: " . totalRemoved,,,, false)
            LogToFile("RemoveUsersFromListWithWonderPick: Finished - no more matches. Removed " . totalRemoved, "GPTestLog.txt")
            return
        }

        if (!GPTest)
            return
    }
}

; GoToSocialScreen - Navigate to the Social screen
GoToSocialScreen() {
    global failSafe

    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        adbClick(143, 518)
        if(FindOrLoseImage(120, 500, 155, 530, , "Social", 0, failSafeTime))
            break
        Delay(5)
        failSafeTime := (A_TickCount - failSafe) // 1000
        CreateStatusMessage("Navigating to Social... " . failSafeTime "/90s")
        if (failSafeTime > 90)
            return false
    }
    return true
}

; ScanFriendListNames - Scan the friend list screen and extract names via OCR
; Returns an array of objects: [{name: "PlayerName", clickY: 195}, ...]
ScanFriendListNames() {
    global winTitle

    visibleFriends := []

    ; Friend list shows up to 3 friends at positions with 95px spacing
    ; Friend 1: Y ~ 195, Friend 2: Y ~ 290, Friend 3: Y ~ 385
    friendPositions := [{slotY: 165, clickY: 195}, {slotY: 260, clickY: 290}, {slotY: 355, clickY: 385}]

    ; Take a screenshot of the current friend list
    screenshotFile := GetTempDirectory() . "\" . winTitle . "_FriendList.png"
    adbTakeScreenshot(screenshotFile)

    ; OCR each friend slot's name region
    ; Expanded search area - scan full width of screen
    for idx, pos in friendPositions {
        ; Full width search: X=0, Y=slotY, Width=280 (full screen), Height=40 (taller)
        friendName := ParseFriendListName(screenshotFile, 0, pos.slotY, 280, 40)

        friendInfo := {name: friendName, clickY: pos.clickY}
        visibleFriends.Push(friendInfo)
    }

    return visibleFriends
}

; ParseFriendListName - Extract a name from a specific region of the friend list screenshot
ParseFriendListName(screenshotFile, x, y, w, h) {
    ; Try multiple scale factors for better OCR accuracy
    blowUp := [200, 300, 400, 500, 600]
    allowedChars := "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789"
    validPattern := "^[a-zA-Z0-9]{3,20}$"

    bestMatch := ""
    Loop, % blowUp.Length() {
        pBitmap := CropAndFormatForOcr(screenshotFile, x, y, w, h, blowUp[A_Index])
        output := GetTextFromBitmap(pBitmap, allowedChars)

        ; Clean up the output
        output := Trim(output)
        output := RegExReplace(output, "\s+", "")  ; Remove whitespace

        ; Log what we're finding for debugging
        if (output != "")
            LogToFile("OCR found at Y=" . y . " scale=" . blowUp[A_Index] . ": '" . output . "'", "GPTestLog.txt")

        if (RegExMatch(output, validPattern)) {
            LogToFile("OCR VALID match: '" . output . "'", "GPTestLog.txt")
            return output
        } else if (StrLen(output) > StrLen(bestMatch)) {
            bestMatch := output
        }
    }

    ; If no valid match, return the longest string we found
    if (bestMatch != "")
        LogToFile("OCR no valid pattern, best effort: '" . bestMatch . "'", "GPTestLog.txt")

    return bestMatch
}

; FuzzyNameMatch - Check if two names match with some tolerance for OCR errors
FuzzyNameMatch(parsedName, targetName) {
    ; Exact match (case insensitive)
    if (parsedName = targetName)
        return true

    ; Use similarity score if available
    similarityScore := SimilarityScore(parsedName, targetName)
    if (similarityScore > 0.75)
        return true

    return false
}

; DoWonderPickCheck - Navigate to wonderpick and do one pick if available
DoWonderPickCheck() {
    global scaleParam

    ; Navigate to main screen first
    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        adbClick(35, 515)  ; Click home
        Sleep, 1000
        if (FindOrLoseImage(191, 393, 211, 411, , "Shop", 0, failSafeTime))
            break
        if (FindOrLoseImage(20, 500, 55, 530, , "Home", 0, failSafeTime))
            break
        failSafeTime := (A_TickCount - failSafe) // 1000
        if (failSafeTime > 30)
            return
    }

    ; Click on WonderPick
    FindImageAndClick(240, 80, 265, 100, , "WonderPick", 59, 429, 2000, 5)

    ; Check if we can do a wonderpick
    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        ; Click on first wonderpick slot
        adbClick(80, 390)
        Sleep, 500
        adbClick(80, 460)  ; backup second slot

        ; Check if no energy
        if(FindOrLoseImage(37, 424, 57, 446, , "noWPenergy", 0, failSafeTime)) {
            CreateStatusMessage("No WonderPick energy!",,,, false)
            Sleep, 1000
            adbClick(137, 505)
            Sleep, 1000
            return
        }

        ; Check if we found a card to pick
        if(FindOrLoseImage(160, 330, 200, 370, , "Card", 0, failSafeTime)) {
            break
        }

        ; Handle any buttons/popups
        if(FindOrLoseImage(240, 80, 265, 100, , "WonderPick", 1, failSafeTime)) {
            clickButton := FindOrLoseImage(100, 367, 190, 480, 100, "Button", 0, failSafeTime)
            if(clickButton) {
                StringSplit, pos, clickButton, `,
                if (scaleParam = 287) {
                    pos2 += 5
                }
                adbClick(pos1, pos2)
                Delay(3)
            }
        }

        failSafeTime := (A_TickCount - failSafe) // 1000
        CreateStatusMessage("Waiting for WonderPick`n(" . failSafeTime . "/30 seconds)")
        if (failSafeTime > 30)
            return
        Sleep, 500
    }

    ; Click the card
    Sleep, 300
    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        adbClick(183, 350)
        if(FindOrLoseImage(160, 330, 200, 370, , "Card", 1, failSafeTime))
            break
        failSafeTime := (A_TickCount - failSafe) // 1000
        if (failSafeTime > 15)
            break
        Sleep, 500
    }

    ; Skip through the card reveal
    Sleep, 2000
    Loop, 10 {
        adbClick(146, 494)
        Sleep, 500
        if (FindOrLoseImage(191, 393, 211, 411, , "Shop", 0))
            break
        if (FindOrLoseImage(240, 80, 265, 100, , "WonderPick", 0))
            break
    }

    CreateStatusMessage("WonderPick check complete!",,,, false)
}

; NavigateToFriendsList - Navigate back to the friends list from anywhere
NavigateToFriendsList() {
    global failSafe

    ; First go home
    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        adbClick(35, 515)
        Sleep, 1000
        if (FindOrLoseImage(20, 500, 55, 530, , "Home", 0, failSafeTime))
            break
        if (FindOrLoseImage(191, 393, 211, 411, , "Shop", 0, failSafeTime))
            break
        failSafeTime := (A_TickCount - failSafe) // 1000
        if (failSafeTime > 30)
            break
    }

    ; Now navigate to Social
    failSafe := A_TickCount
    failSafeTime := 0
    Loop {
        adbClick(143, 518)
        if(FindOrLoseImage(120, 500, 155, 530, , "Social", 0, failSafeTime))
            break
        Delay(5)
        failSafeTime := (A_TickCount - failSafe) // 1000
        if (failSafeTime > 30)
            break
    }

    ; Go to friends
    FindImageAndClick(226, 100, 270, 135, , "Add", 38, 460, 500)
    Delay(3)
}

; ScrollFriendList - Perform a scroll on the friends list
ScrollFriendList() {
    X := 138
    Y1 := 380
    Y2 := 200
    Delay(10)
    adbSwipe(X . " " . Y1 . " " . X . " " . Y2 . " " . 300)
    Sleep, 1000
}

; Attempts to extract a friend accounts's code and name from the screen, by taking screenshot and running OCR on specific regions.
ParseFriendInfo(ByRef friendCode, ByRef friendName, ByRef parseFriendCodeResult, ByRef parseFriendNameResult, includesIdsAndNames := False) {
    ; ------------------------------------------------------------------------------
    ; The function has a fail-safe mechanism to stop after 5 seconds.
    ;
    ; Parameters:
    ;   friendCode (ByRef String)          - A reference to store the extracted friend code.
    ;   friendName (ByRef String)          - A reference to store the extracted friend name.
    ;   parseFriendCodeResult (ByRef Bool) - A reference to store the result of parsing the friend code.
    ;   parseFriendNameResult (ByRef Bool) - A reference to store the result of parsing the friend name.
    ;   includesIdsAndNames (Bool)         - A flag indicating whether to parse the friend name, in addition to the code (default: False).
    ;
    ; Returns:
    ;   (Boolean) - True if EITHER the friend code OR name were successfully parsed, false otherwise.
    ; ------------------------------------------------------------------------------
    ; Initialize variables
    failSafe := A_TickCount
    failSafeTime := 0
    friendCode := ""
    friendName := ""
    parseFriendCodeResult := False
    parseFriendNameResult := False

    Loop {
        ; Grab screenshot via Adb
        fullScreenshotFile := GetTempDirectory() . "\" .  winTitle . "_FriendProfile.png"
        adbTakeScreenshot(fullScreenshotFile)

        ; Parse friend identifiers
        if (!parseFriendCodeResult)
            parseFriendCodeResult := ParseFriendInfoLoop(fullScreenshotFile, 265, 57, 240, 28, "0123456789", "^\d{14,17}$", friendCode)
        if (includesIdsAndNames && !parseFriendNameResult)
            parseFriendNameResult := ParseFriendInfoLoop(fullScreenshotFile, 107, 427, 325, 46, "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789", "^[a-zA-Z0-9]{5,20}$", friendName)
        if (parseFriendCodeResult && (!includesIdsAndNames || parseFriendNameResult))
            break

        ; Break and fail if this take more than 5 seconds
        failSafeTime := (A_TickCount - failSafe) // 1000
        if (failSafeTime > 5)
            break
    }

    ; Return true if we were able to parse EITHER the code OR the name
    return parseFriendCodeResult || (includesIdsAndNames && parseFriendNameResult)
}

; Attempts to extract and validate text from a specified region of a screenshot using OCR.
ParseFriendInfoLoop(screenshotFile, x, y, w, h, allowedChars, validPattern, ByRef output) {
    ; ------------------------------------------------------------------------------
    ; The function crops, formats, and scales the screenshot, runs OCR,
    ; and checks if the result matches a valid pattern. It loops through multiple
    ; scaling factors to improve OCR accuracy.
    ;
    ; Parameters:
    ;   screenshotFile (String)   - The path to the screenshot file to process.
    ;   x (Integer)               - The X-coordinate of the crop region.
    ;   y (Integer)               - The Y-coordinate of the crop region.
    ;   w (Integer)               - The width of the crop region.
    ;   h (Integer)               - The height of the crop region.
    ;   allowedChars (String)     - A list of allowed characters for OCR filtering.
    ;   validPattern (String)     - A regular expression pattern to validate the OCR result.
    ;   output (ByRef)            - A reference variable to store the OCR output text.
    ;
    ; Returns:
    ;   (Boolean) - True if valid text was found and matched the pattern, false otherwise.
    ; ------------------------------------------------------------------------------
    success := False
    blowUp := [200, 500, 1000, 2000, 100, 250, 300, 350, 400, 450, 550, 600, 700, 800, 900]
    Loop, % blowUp.Length() {
        ; Get the formatted pBitmap
        pBitmap := CropAndFormatForOcr(screenshotFile, x, y, w, h, blowUp[A_Index])
        ; Run OCR
        output := GetTextFromBitmap(pBitmap, allowedChars)
        ; Validate result
        if (RegExMatch(output, validPattern)) {
            success := True
            break
        }
    }
    return success
}

; FriendAccount class that holds information about a friend account, including the account's code (ID) and name.
class FriendAccount {
    ; ------------------------------------------------------------------------------
    ; Properties:
    ;   Code (String)    - The unique identifier (ID) of the friend account.
    ;   Name (String)    - The name associated with the friend account.
    ;
    ; Methods:
    ;   __New(Code, Name) - Constructor method to initialize the friend account
    ;                       with a code and name.
    ;   ToString()        - Returns a string representation of the friend account.
    ;                       If both the code and name are provided, it returns
    ;                       "Name (Code)". If only one is available, it returns
    ;                       that value, and if both are missing, it returns "Null".
    ; ------------------------------------------------------------------------------
    __New(Code, Name) {
        this.Code := Code
        this.Name := Name
    }

    ToString() {
        if (this.Name != "" && this.Code != "")
            return this.Name . " (" . this.Code . ")"
        if (this.Name == "" && this.Code != "")
            return this.Code
        if (this.Name != "" && this.Code == "")
            return this.Name
        return "Null"
    }
}

; Reads a file containing friend account information, parses it, and returns a list of FriendAccount objects
GetFriendAccountsFromFile(filePath, ByRef includesIdsAndNames) {
    ; ------------------------------------------------------------------------------
    ; The function also determines if the file includes both IDs and names for each friend account.
    ; Friend accounts are only added to the output list if star and pack requirements are met.
    ;
    ; Parameters:
    ;   filePath (String)           - The path to the file to read.
    ;   includesIdsAndNames (ByRef) - A reference variable that will be set to true if the file includes both friend IDs and names.
    ;
    ; Returns:
    ;   (Array) - An array of FriendAccount objects, parsed from the file.
    ; ------------------------------------------------------------------------------
    global minStars, minStarsA2b
    friendList := []  ; Create an empty array
    includesIdsAndNames := false

    FileRead, fileContent, %filePath%
    if (ErrorLevel) {
        MsgBox, Failed to read file!
        return friendList  ; Return empty array if file can't be read
    }

    Loop, Parse, fileContent, `n, `r  ; Loop through lines in file
    {
        line := A_LoopField
        if (line = "" || line ~= "^\s*$")  ; Skip empty lines
            continue

        friendCode := ""
        friendName := ""
        twoStarCount := ""
        packName := ""

        if InStr(line, " | ") {
            parts := StrSplit(line, " | ") ; Split by " | "

            ; Check for ID and Name parts
            friendCode := Trim(parts[1])
            friendName := Trim(parts[2])
            if (friendCode != "" && friendName != "")
                includesIdsAndNames := true

            ; Extract the number before "/" in TwoStarCount
            twoStarCount := RegExReplace(parts[3], "\D.*", "")  ; Remove everything after the first non-digit

            packName := Trim(parts[4])
        } else {
            friendCode := Trim(line)
        }

        friendCode := RegExReplace(friendCode, "\D") ; Clean the string (just in case)
        if (!RegExMatch(friendCode, "^\d{14,17}$")) ; Only accept valid IDs
            friendCode := ""
        if (friendCode = "" && friendName = "")
            continue

        ; Trim spaces and create a FriendAccount object
        if (twoStarCount == ""
            || (packName != "Shining" && twoStarCount >= minStars)
            || (packName == "Shining" && twoStarCount >= minStarsA2b)
            || (packName == "" && (twoStarCount >= minStars || twoStarCount >= minStarsA2b)) ) {
            friend := new FriendAccount(friendCode, friendName)
            friendList.Push(friend)  ; Add to array
        }
    }
    return friendList
}

; Compares two friend accounts to check if they match based on their code and/or name.
MatchFriendAccounts(friend1, friend2, ByRef similarityScore := 1) {
    ; ------------------------------------------------------------------------------
    ; The similarity score between the two accounts is calculated and used to determine a match.
    ; If both the code and name match with a high enough similarity score, the function returns true.
    ;
    ; Parameters:
    ;   friend1 (Object)           - The first friend account to compare.
    ;   friend2 (Object)           - The second friend account to compare.
    ;   similarityScore (ByRef)    - A reference to store the calculated similarity score
    ;                                (defaults to 1).
    ;
    ; Returns:
    ;   (Bool) - True if the accounts match based on the similarity score, false otherwise.
    ; ------------------------------------------------------------------------------
    if (friend1.Code != "" && friend2.Code != "") {
        similarityScore := SimilarityScore(friend1.Code, friend2.Code)
        if (similarityScore > 0.6)
            return true
    }
    if (friend1.Name != "" && friend2.Name != "") {
        similarityScore := SimilarityScore(friend1.Name, friend2.Name)
        if (similarityScore > 0.8) {
            if (friend1.Code != "" && friend2.Code != "") {
                similarityScore := (SimilarityScore(friend1.Code, friend2.Code) + SimilarityScore(friend1.Name, friend2.Name)) / 2
                if (similarityScore > 0.7)
                    return true
            }
            else
                return true
        }
    }
    return false
}

; Checks if a given friend account exists in the friend list. If a match is found, the matching friend's information is returned via the matchedFriend parameter.
IsFriendAccountInList(inputFriend, friendList, ByRef matchedFriend) {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   inputFriend (String)  - The account to search for in the list.
    ;   friendList (Array)    - The list of friends to search through.
    ;   matchedFriend (ByRef) - The matching friend's account information, if found (passed by reference).
    ;
    ; Returns:
    ;   (Bool) - True if a matching friend account is found, false otherwise.
    ; ------------------------------------------------------------------------------
    matchedFriend := ""
    for index, friend in friendList {
        if (MatchFriendAccounts(inputFriend, friend)) {
            matchedFriend := friend
            return true
        }
    }
    return false
}

; Checks if an account has already been added to the friend list. If not, it adds the account to the list.
IsRecentlyCheckedAccount(inputFriend, ByRef friendList) {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   inputFriend (String) - The account to check against the list.
    ;   friendList (Array)   - The list of friends to check the account against.
    ;
    ; Returns:
    ;   (Bool) - True if the account is already in the list, false otherwise.
    ; ------------------------------------------------------------------------------
    if (inputFriend == "") {
        return false
    }

    ; Check if the account is already in the list
    if (IsFriendAccountInList(inputFriend, friendList, matchedFriend)) {
        return true
    }

    ; Add the account to the end of the list
    friendList.Push(inputFriend)

    return false  ; Account was not found and has been added
}

; Handles level up notifications during gameplay by clicking through them if detected.
LevelUp() {
    ; ------------------------------------------------------------------------------
    ; Checks if a level up notification is displayed and clicks through it.
    ; Uses the "LevelUp" image to detect the notification, then finds and clicks
    ; the confirmation button.
    ;
    ; Returns:
    ;   None - Function executes actions and returns
    ; ------------------------------------------------------------------------------
    global scaleParam
    Leveled := FindOrLoseImage(100, 86, 167, 116, , "LevelUp", 0)
    if(Leveled) {
        clickButton := FindOrLoseImage(75, 340, 195, 530, 80, "Button", 0)
        StringSplit, pos, clickButton, `,  ; Split at ", "
        if (scaleParam = 287) {
            pos2 += 5
        }
        adbClick(pos1, pos2)
    }
    Delay(1)
}

; Retrieves the path to the temporary directory for the script. If the directory does not exist, it is created.
GetTempDirectory() {
    ; ------------------------------------------------------------------------------
    ; Returns:
    ;   (String) - The full path to the temporary directory.
    ; ------------------------------------------------------------------------------
    tempDir := A_ScriptDir . "\temp"
    if !FileExist(tempDir)
        FileCreateDir, %tempDir%
    return tempDir
}

; Wrapper for adbClick with optional bounding box debugging display.
adbClick_wbb(X,Y)  {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   X (Int) - X-coordinate to click
    ;   Y (Int) - Y-coordinate to click
    ;
    ; If dbg_bbox global is enabled, shows a bounding box before clicking.
    ; ------------------------------------------------------------------------------
    global dbg_bbox, dbg_bboxNpause
    if(dbg_bbox)
        bboxAndPause_click(X, Y, dbg_bboxNpause)
    adbClick(X,Y)
}

; Displays a bounding box at click location for debugging purposes.
bboxAndPause_click(X, Y, doPause := False) {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   X (Int)       - X-coordinate center of box
    ;   Y (Int)       - Y-coordinate center of box
    ;   doPause (Bool) - Whether to pause execution after displaying box
    ;
    ; Shows a small box around the click point and optionally pauses for debugging.
    ; ------------------------------------------------------------------------------
    global winTitle
    CreateStatusMessage("Clicking X " . X . " Y " . Y,,,, false)

    color := "BackgroundBlue"

    bboxDraw(X-5, Y-5, X+5, Y+5, color)

    if (doPause) {
        Pause
    }

    if GetKeyState("F4", "P") {
        Pause
    }
    Gui, BoundingBox%winTitle%:Destroy
}

; Draws a rectangular bounding box overlay on the screen for debugging.
bboxDraw(X1, Y1, X2, Y2, color) {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   X1 (Int)    - Top-left X coordinate
    ;   Y1 (Int)    - Top-left Y coordinate
    ;   X2 (Int)    - Bottom-right X coordinate
    ;   Y2 (Int)    - Bottom-right Y coordinate
    ;   color (Str) - Color name for the box borders
    ;
    ; Creates a transparent GUI overlay with colored borders to show a region.
    ; ------------------------------------------------------------------------------
    global winTitle
    WinGetPos, xwin, ywin, Width, Height, %winTitle%
    BoxWidth := X2-X1
    BoxHeight := Y2-Y1
    ; Create a GUI
    Gui, BoundingBox%winTitle%:+AlwaysOnTop +ToolWindow -Caption +E0x20
    Gui, BoundingBox%winTitle%:Color, 123456
    Gui, BoundingBox%winTitle%:+LastFound  ; Make the GUI window the last found window for use by the line below. (straght from documentation)
    WinSet, TransColor, 123456 ; Makes that specific color transparent in the gui

    ; Create the borders and show
    Gui, BoundingBox%winTitle%:Add, Progress, x0 y0 w%BoxWidth% h2 %color%
    Gui, BoundingBox%winTitle%:Add, Progress, x0 y0 w2 h%BoxHeight% %color%
    Gui, BoundingBox%winTitle%:Add, Progress, x%BoxWidth% y0 w2 h%BoxHeight% %color%
    Gui, BoundingBox%winTitle%:Add, Progress, x0 y%BoxHeight% w%BoxWidth% h2 %color%

    xshow := X1+xwin
    yshow := Y1+ywin
    Gui, BoundingBox%winTitle%:Show, x%xshow% y%yshow% NoActivate
    Sleep, 100
}

; Displays a bounding box for image search debugging.
bboxAndPause_immage(X1, Y1, X2, Y2, pNeedleObj, vret := False, doPause := False) {
    ; ------------------------------------------------------------------------------
    ; Parameters:
    ;   X1 (Int)         - Top-left X coordinate of search region
    ;   Y1 (Int)         - Top-left Y coordinate of search region
    ;   X2 (Int)         - Bottom-right X coordinate of search region
    ;   Y2 (Int)         - Bottom-right Y coordinate of search region
    ;   pNeedleObj (Obj) - Needle object containing Name property
    ;   vret (Mixed)     - Return value from image search (for color coding)
    ;   doPause (Bool)   - Whether to pause if image found
    ;
    ; Shows green box if image found, red if not found.
    ; ------------------------------------------------------------------------------
    global winTitle
    CreateStatusMessage("Searching " . pNeedleObj.Name . " returns " . vret,,,, false)

    if(vret>0) {
        color := "BackgroundGreen"
    } else {
        color := "BackgroundRed"
    }

    bboxDraw(X1, Y1, X2, Y2, color)

    if (doPause && vret) {
        Pause
    }

    if GetKeyState("F4", "P") {
        Pause
    }
    Gui, BoundingBox%winTitle%:Destroy
}

; Wrapper for Gdip_ImageSearch with bounding box debugging and title bar offset adjustment.
Gdip_ImageSearch_wbb(pBitmapHaystack,pNeedle,ByRef OutputList=""
,OuterX1=0,OuterY1=0,OuterX2=0,OuterY2=0,Variation=0,Trans=""
,SearchDirection=1,Instances=1,LineDelim="`n",CoordDelim=",") {
    ; ------------------------------------------------------------------------------
    ; Wrapper around Gdip_ImageSearch that:
    ;   1. Adjusts Y coordinates for title bar height
    ;   2. Optionally shows debug bounding box if dbg_bbox is enabled
    ;
    ; Parameters: Same as Gdip_ImageSearch
    ; Returns: Result from Gdip_ImageSearch
    ; ------------------------------------------------------------------------------
    global titleHeight, dbg_bbox, dbg_bboxNpause
    yBias := titleHeight - 45
    vret := Gdip_ImageSearch(pBitmapHaystack,pNeedle.needle,OutputList,OuterX1,OuterY1+yBias,OuterX2,OuterY2+yBias,Variation,Trans,SearchDirection,Instances,LineDelim,CoordDelim)
    if(dbg_bbox)
        bboxAndPause_immage(OuterX1, OuterY1+yBias, OuterX2, OuterY2+yBias, pNeedle, vret, dbg_bboxNpause)
    return vret
}
